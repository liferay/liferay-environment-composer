import groovy.json.JsonSlurper

import java.time.format.DateTimeFormatter

plugins {
	id "com.avast.gradle.docker-compose" version "0.17.10"
	id "docker-common"
	id "docker-export-backup"
	id "docker-import-backup"
	id "docker-liferay-bundle"
}

file('.env').withOutputStream {
	BufferedOutputStream envFileOutputStream ->

	config.environmentMap.forEach {
		key, value ->

		envFileOutputStream << key << "=" << value << "\n"
	}
}

gradle.liferayWorkspace {
	dockerImageId = config.liferayDockerImageId
}

tasks.register("shareWorkspace", Zip) {
	archiveFileName = provider {
		DateTimeFormatter formatter = DateTimeFormatter.
		ofPattern("yyyyMMdd.HHmmss").
		withZone(ZoneId.systemDefault())

		"workspace_${config.namespace}_${formatter.format(Instant.now())}.zip"
	}

	destinationDirectory = file 'shared_workspaces'

	doLast {
		logger.lifecycle "\nWorkspace zip: ${archiveFile.get()}\n"
	}

	exclude ".gitkeep"
	exclude ".gitignore"
	exclude ".git"
	exclude ".gradle"
	exclude "build"
	exclude "buildSrc/.gradle"
	exclude "buildSrc/build"
	exclude "lxc"
	exclude "shared_workspaces"
	exclude "tmp"

	if (config.dataDirectory != null) {
		exclude {
			return (it.relativePath.pathString.startsWith("data/") || it.relativePath.pathString.startsWith("exported_data/")) &&
				!it.relativePath.pathString.startsWith(config.dataDirectory)
		}
	}

	from '.'

	include "**/*"

	outputs.upToDateWhen {
		false
	}
}

tasks.register("start") {
	dependsOn ":composeUp"
}

tasks.register("stop") {
	dependsOn ":composeDown"
}

clean {
	dependsOn ":cleanPrepareHotfixes"
}

dockerDeploy {
	if (config.useLiferay) {
		dependsOn ":prepareHotfixes"
	}
}

composeUp {
	if (config.useLiferay) {
		dependsOn ":buildDockerImage"
	}

	dependsOn checkForLiferayLicense
	dependsOn importDatabaseDumps
	dependsOn importDocumentLibrary

	doFirst {
		println "Using config:\n${config}"
	}

	onlyIf("there are compose files") {
		!config.composeFiles.isEmpty()
	}

	finalizedBy listAdminUsers
}

dockerCompose {
	// DEBUG: Set to true to print container startup output to the console
	captureContainersOutput = false

	// DEBUG: Uncomment to dump the container logs to a directory for inspection
	// captureContainersOutputToFiles = project.file('containerLogs')

	environment.putAll config.environmentMap

	projectName = config.namespace

	removeVolumes = config.clearVolumeData

	tcpPortsToIgnoreWhenWaiting = [8000, 11311]
	useComposeFiles = config.composeFiles

	// DEBUG: Set to true if container startup is failing
	retainContainersOnStartupFailure = false
}