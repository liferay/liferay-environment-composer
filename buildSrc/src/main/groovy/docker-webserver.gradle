import groovy.text.SimpleTemplateEngine

SimpleTemplateEngine templateEngine = new SimpleTemplateEngine()

tasks.register("deleteModSecurityConfigFiles") {
	doLast {
		String webserverBuildConfigDir = "compose-recipes/webserver/build/config"

		for (filepath in ['crs-setup.conf', 'rules']) {
			project.delete("${webserverBuildConfigDir}/${filepath}")
		}
	}
}

tasks.register("prepareModSecurityConfigFiles") {
	onlyIf("using a webserver") {
		config.useWebserver
	}
	onlyIf("ModSecurity is enabled") {
		config.modSecurityEnabled
	}

	doLast {
		String containerName = 'modsecurity'

		exec {
			commandLine 'docker', 'create', '--name', containerName, 'owasp/modsecurity-crs:nginx'
		}

		File webserverBuildConfigDir = "compose-recipes/webserver/build/config/" as File

		mkdir webserverBuildConfigDir

		exec {
			commandLine 'docker', 'cp', "${containerName}:/etc/modsecurity.d/owasp-crs/crs-setup.conf", webserverBuildConfigDir
		}

		exec {
			commandLine 'docker', 'cp', "${containerName}:/etc/modsecurity.d/owasp-crs/rules", webserverBuildConfigDir
		}

		exec { commandLine 'docker', 'rm', containerName }
	}
}

tasks.register("prepareSelfSignedCert", Copy) {
	outputs.upToDateWhen {
		false
	}

	onlyIf("using a webserver with HTTPS") {
		config.useWebserver && config.webserverProtocol == "https"
	}

	List<String> hostnames = config.webserverHostnames
	.split(" ")
	.toList()

	File certDir = "compose-recipes/webserver/build/ssl" as File
	File opensslConfigFile = "$certDir/openssl.cnf" as File
	File certFile = "$certDir/server.crt" as File
	File keyFile = "$certDir/server.key" as File

	String sanEntries = hostnames
	.withIndex()
	.collect() {
		host, i -> "DNS.${i + 1} = ${host}"
	}
	.join('\n')

	from 'templates/webserver/ssl'

	include 'openssl.cnf.template'
	rename 'openssl.cnf.template', 'openssl.cnf'

	expand (hostname: hostnames[0], sanEntries: sanEntries)

	into certDir

	doLast {
		exec {
			commandLine 'openssl', 'req', '-x509',
			'-nodes',
			'-days', '365',
			'-newkey', 'rsa:2048',
			'-keyout', keyFile,
			'-out', certFile,
			'-config', opensslConfigFile
		}
	}
}

tasks.register("prepareWebserverConfig", Copy) {
	onlyIf("using a webserver") {
		config.useWebserver
	}

	outputs.upToDateWhen {
		false
	}

	File webserverConfigTemplatesDir = "templates/webserver/config" as File
	String protocol = config.webserverProtocol

	File upstreamTemplateFile = config.useClustering
	? file("${webserverConfigTemplatesDir}/upstream.cluster.conf.template")
	: file("${webserverConfigTemplatesDir}/upstream.conf.template")

	from webserverConfigTemplatesDir
	include "base.conf.template"
	into "compose-recipes/webserver/build/config"
	rename "base.conf.template", "default.conf"

	expand(
		webserverConfig: file("$webserverConfigTemplatesDir/${protocol}.conf.template").text,
		webserverHostnames: config.webserverHostnames,
		upstreamConf: templateEngine.createTemplate(upstreamTemplateFile.text).make([protocol: protocol])
	)
}