deploy {
	doLast {
		String projectName = "{{PROJECT_NAME}}"
		String composedArchive = "${destinationDir}/${liferay.deployedFileNameClosure.call(jar)}"

		List<String> commandArgs = ["docker", "compose", "-p", projectName, "cp"]

		ByteArrayOutputStream out = new ByteArrayOutputStream()

		project.exec {
			commandLine "docker", "info", "--format", "json"
			standardOutput = out
		}

		groovy.json.JsonSlurper jsonSlurper = new groovy.json.JsonSlurper()

		Object dockerInfo = jsonSlurper.parse(out.toByteArray())

		boolean rootless = dockerInfo['SecurityOptions'].any {
			it.contains("name=rootless")
		}

		if (!rootless) {
			commandArgs.add("--archive")
		}

		commandArgs.addAll([composedArchive, "liferay:/opt/liferay/deploy"])

		logger.lifecycle("Copying jar to Docker container...")
		logger.lifecycle("Using command: ${commandArgs.join(" ")}")

		project.exec {
			commandLine commandArgs
		}
	}
}
